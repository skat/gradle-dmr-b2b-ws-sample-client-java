buildscript {
    ext {
        xjcVersion= '3.0.5'
        jaxbOutputDir = "src/generated/java"
        jaxb2BasicsVersion = '0.11.0'
        cxfVersion = '3.3.6'
    }
}

plugins {
    id 'java'
}

repositories {
    mavenLocal()
    maven {
        url = uri('https://repo.maven.apache.org/maven2/')
    }
}

group = 'dk.skat.dmr.b2b.sample'
version = '1.0'
description = 'dmr-b2b-ws-sample-client-java'
java.sourceCompatibility = JavaVersion.VERSION_1_8

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

configurations {
    cxf
}

task wsdl2java(type: JavaExec) {
    ext {
        outputDir = file("$jaxbOutputDir")
    }
    systemProperties  = ['javax.xml.accessExternalSchema': 'file' , 'file.encoding':'UTF8']
    outputs.upToDateWhen { false }
    outputs.dir outputDir
    main = 'org.apache.cxf.tools.wsdlto.WSDLToJava'
    classpath = project.configurations.cxf
    args '-d', outputDir
    //args '-fe', 'jaxws21'
    args '-client'
    args '-verbose'
    //args '-validate'
    args '-mark-generated'
    //args '-xjc-X'
    args '-xjc-Xfluent-api'
    args '-xjc-Xts'
    args '-xjc-XhashCode'
    args '-xjc-Xequals'
    //args '-b',"$projectDir/src/main/resources/jaxws-custom-bindings.xjb"
    args "$projectDir/src/main/resources/wsdls/USImportoerService.wsdl"

    doFirst {
        println "----- Starting cxf jaxb2 files generation -----"

    }
    doLast {
        println "----- cxf jaxb2 files generated -----"
    }
}

dependencies {
    cxf "org.apache.cxf:cxf-tools-wsdlto-core:$cxfVersion"
    cxf "org.apache.cxf:cxf-tools-wsdlto-frontend-jaxws:$cxfVersion"
    cxf "org.apache.cxf:cxf-tools-wsdlto-databinding-jaxb:$cxfVersion"
    cxf "org.jvnet.jaxb2_commons:jaxb2-fluent-api:3.0"
    cxf "org.apache.cxf.xjcplugins:cxf-xjc-ts:$xjcVersion"
    cxf "org.jvnet.jaxb2_commons:jaxb2-basics:$jaxb2BasicsVersion"

    testImplementation 'junit:junit:4.13.1'

    compile 'org.apache.cxf:cxf-rt-frontend-jaxws:3.1.9'
    compile 'org.apache.cxf:cxf-rt-transports-http:3.1.9'
    compile 'org.apache.cxf:cxf-rt-transports-http-jetty:3.1.9'
    compile 'org.apache.cxf:cxf-rt-ws-security:3.1.9'
    compile 'org.apache.cxf:cxf-rt-ws-policy:3.1.9'

    compile 'org.springframework:spring-core:4.3.21.RELEASE'
    compile 'org.springframework:spring-beans:4.3.21.RELEASE'
    compile 'org.springframework:spring-context:4.3.21.RELEASE'
    compile 'org.slf4j:jcl-over-slf4j:1.7.22'

    compile "org.apache.cxf.xjc-utils:cxf-xjc-runtime:$xjcVersion"
    compile "org.jvnet.jaxb2_commons:jaxb2-basics-runtime:$jaxb2BasicsVersion"
}

compileJava {
    dependsOn wsdl2java
    sourceSets {
        main {
            java {
                srcDir file("$jaxbOutputDir")
            }
        }
    }
}

test {
    systemProperty "dk.skat.dmr.b2b.sample.P12_PASSPHRASE", project.getProperty("dk.skat.dmr.b2b.sample.P12_PASSPHRASE")
    systemProperty "dk.skat.dmr.b2b.sample.USImportoerService.ENDPOINT", project.getProperty("dk.skat.dmr.b2b.sample.USImportoerService.ENDPOINT")
    if(project.hasProperty("dk.skat.dmr.b2b.sample.TXID_PREFIX")) {
        systemProperty "dk.skat.dmr.b2b.sample.TXID_PREFIX", project.getProperty("dk.skat.dmr.b2b.sample.TXID_PREFIX")
    }
}
